plugins {
    id 'idea'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.spotbugs' version '1.6.2' apply false
    id 'io.franzbecker.gradle-lombok' version '1.14' apply false
    id 'com.jfrog.bintray' version '1.7.3' apply false
}

allprojects {
    repositories {
        jcenter()
    }
}

task build {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

task docs(type: Javadoc) {
    destinationDir = file("$buildDir/docs/all")
    exclude '**/internal/**'
    exclude '**/legacy/**'
}

subprojects {
    afterEvaluate {
        if(plugins.hasPlugin(JavaPlugin)) {

            // configuration here
            rootProject.tasks.docs {
                source += files(sourceSets.collect { srcSet -> srcSet.allJava })
                classpath += files(sourceSets*.compileClasspath)
            }

        }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
