plugins {
    id 'java'
    id 'groovy'
    id 'checkstyle'
    id 'findbugs'
    id 'pmd'
    id 'codenarc'
    id 'jacoco'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.14.0'
}

version = '0.1.0'
ext {
    gdxVersion = '1.9.6'
}
sourceSets {
    demo {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    demoCompile.extendsFrom compile
    demoRuntime.extendsFrom runtime
}

task demo(type: JavaExec) {
    classpath = sourceSets.demo.runtimeClasspath

    main = 'com.github.jaystgelais.jrpg.demo.Demo'
}

checkstyle {
    sourceSets = [sourceSets.main]
}

findbugs {
    sourceSets = [sourceSets.main]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

codenarc {
    configFile = rootProject.file('codenarcrule.groovy')
    reportFormat = System.properties['codeNarcOutput'] ?: 'html'
}

test.finalizedBy jacocoTestReport

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

    testCompile "org.codehaus.groovy:groovy-all:2.4.11"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testRuntime "cglib:cglib-nodep:3.2.5"
    testRuntime "org.objenesis:objenesis:2.5.1"
}
